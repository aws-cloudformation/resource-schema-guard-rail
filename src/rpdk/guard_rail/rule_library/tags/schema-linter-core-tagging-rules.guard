

rule check_if_taggable_is_used {
    taggable !exists
    <<
    {
        "result": "WARNING",
        "check_id": "TAG001",
        "message": "`taggable` is deprecated, please used `tagging` property"
    }
    >>
}

rule ensure_tagging_is_specified {
    tagging exists
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "TAG002",
        "message": "`tagging` MUST be specified"
    }
    >>

    when tagging exists {
        tagging.taggable == true
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "TAG015",
            "message": "`tagging.taggable` MUST be true"
        }
        >>
    }
}

rule ensure_taggable_and_tagging_do_not_coexist when taggable exists {
    tagging !exists
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "TAG003",
        "message": "`taggable` and `tagging` MUST NOT coexist"
    }
    >>
}

rule ensure_property_tags_exists_v1 when taggable exists {
    when taggable == true {
        properties.Tags exists
        <<
        {
            "result": "WARNING",
            "check_id": "TAG004",
            "message": "Resource MUST implement Tags property if `taggable` is true"
        }
        >>
    }
}

rule ensure_property_tags_exists_v2 when tagging exists {
    tagging is_struct
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "TAG005",
        "message": "`tagging` MUST BE a struct"
    }
    >>

    tagging.taggable exists
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "TAG006",
        "message": "`taggable` MUST BE specified when `tagging` is provided"
    }
    >>

    when tagging.taggable == true {

        tagging.tagOnCreate exists
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "TAG007",
            "message": "Resource MUST provide `tagOnCreate` {true|false} if `tagging.taggable` is true"
        }
        >>

        tagging.tagUpdatable exists
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "TAG008",
            "message": "Resource MUST provide `tagUpdatable` {true|false} if `tagging.taggable` is true"
        }
        >>

        tagging.cloudFormationSystemTags exists
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "TAG009",
            "message": "Resource MUST provide `cloudFormationSystemTags` {true|false} if `tagging.taggable` is true"
        }
        >>

        tagging.tagProperty exists
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "TAG010",
            "message": "Resource MUST provide `tagProperty` {/properties/Tags} if `tagging.taggable` is true"
        }
        >>

        properties.Tags exists
        <<
        {
            "result": "WARNING",
            "check_id": "TAG011",
            "message": "Resource MUST implement Tags property if `tagging.taggable` is true"
        }
        >>

        when tagging.tagProperty exists {
            let paths = paths
            tagging.tagProperty IN %paths
            <<
            {
                "result": "NON_COMPLIANT",
                "check_id": "TAG013",
                "message": "`tagProperty` MUST specify property defined in the schema"
            }
            >>

            when writeOnlyProperties exists {
                tagging.tagProperty !IN writeOnlyProperties
                <<
                {
                    "result": "NON_COMPLIANT",
                    "check_id": "TAG014",
                    "message": "`tagProperty` MUST NOT be a part of `writeOnlyProperties`"
                }
                >>
            }
            when createOnlyProperties exists {
                tagging.tagProperty !IN createOnlyProperties
                <<
                {
                    "result": "WARNING",
                    "check_id": "TAG017",
                    "message": "`tagProperty` MUST NOT be a part of `createOnlyProperties`"
                }
                >>
                when tagging.tagProperty IN createOnlyProperties {
                    tagging.tagUpdatable == false
                    <<
                    {
                        "result": "WARNING",
                        "check_id": "TAG018",
                        "message": "`tagProperty` MUST NOT be a part of `createOnlyProperties` when `tagUpdatable` is true"
                    }
                    >>
                }
            }
        }
        tagging.permissions exists
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "TAG012",
            "message": "Resource MUST provide `permissions` if `tagging.taggable` is true"
        }
        >>
    }

    when tagging.taggable == false {
        TaggingPath !exists
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "TAG016",
            "message": "`tagging.taggable` MUST be true when Tagging Property is defined in the schema"
        }
        >>
    }
}
