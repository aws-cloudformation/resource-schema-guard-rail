rule ensure_primary_identifier_exists_and_not_empty
{
    primaryIdentifier exists
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "PID001",
        "message": "primaryIdentifier MUST exist"
    }
    >>
    primaryIdentifier not empty
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "PID002",
        "message": "primaryIdentifier MUST contain values"
    }
    >>

    when primaryIdentifier exists {
        when writeOnlyProperties exists {
            let write = writeOnlyProperties
            primaryIdentifier[*] {
                this !IN %write
                <<
                {
                    "result": "NON_COMPLIANT",
                    "check_id": "PID004",
                    "message": "primaryIdentifier MUST NOT be part of writeOnlyProperties"
                }
                >>
            }
        }
    }
}

rule ensure_primary_identifier_is_read_or_create_only when ensure_primary_identifier_exists_and_not_empty
{
    when readOnlyProperties exists {
        let read = readOnlyProperties
        when createOnlyProperties exists {
            let create = createOnlyProperties
            primaryIdentifier[*] {
                this IN %read OR
                this IN %create
                <<
                {
                    "result": "NON_COMPLIANT",
                    "check_id": "PID003",
                    "message": "primaryIdentifier MUST be either readOnly or createOnly"
                }
                >>
            }
        }

        when createOnlyProperties !exists {
            primaryIdentifier[*] {
                this IN %read
                <<
                {
                    "result": "NON_COMPLIANT",
                    "check_id": "PID003",
                    "message": "primaryIdentifier MUST be either readOnly or createOnly"
                }
                >>
            }
        }
    }

    when readOnlyProperties !exists {
        createOnlyProperties exists
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "PID003",
            "message": "primaryIdentifier MUST be either readOnly or createOnly"
        }
        >>

        when createOnlyProperties exists {
            let create = createOnlyProperties
            primaryIdentifier[*] {
                this IN %create
                <<
                {
                    "result": "NON_COMPLIANT",
                    "check_id": "PID003",
                    "message": "primaryIdentifier MUST be either readOnly or createOnly"
                }
                >>
            }
        }
    }
}



rule ensure_create_and_read_only_intersection_is_empty
{
    when readOnlyProperties exists {
        when createOnlyProperties exists {
            let create = createOnlyProperties
            readOnlyProperties[*] {
                this !IN %create
                <<
                {
                    "result": "NON_COMPLIANT",
                    "check_id": "PR001",
                    "message": "read/createOnlyProperties MUST NOT have common properties"
                }
                >>
            }
        }
    }
}

rule ensure_write_and_read_only_intersection_is_empty
{
    when readOnlyProperties exists {
        when writeOnlyProperties exists {
            let write = writeOnlyProperties
            readOnlyProperties[*] {
                this !IN %write
                <<
                {
                    "result": "NON_COMPLIANT",
                    "check_id": "PR004",
                    "message": "write/readOnlyProperties MUST NOT have common properties"
                }
                >>
            }
        }
    }
}

rule verify_property_notation
{
    let paths = paths
    when primaryIdentifier exists {
        primaryIdentifier[*] {
            this IN %paths
            <<
            {
                "result": "NON_COMPLIANT",
                "check_id": "PR005",
                "message": "primaryIdentifier MUST have properties defined in the schema"
            }
            >>
        }
    }

    when createOnlyProperties exists {
        createOnlyProperties[*] {
            this IN %paths
            <<
            {
                "result": "NON_COMPLIANT",
                "check_id": "PR006",
                "message": "createOnlyProperties MUST have properties defined in the schema"
            }
            >>
        }
    }

    when readOnlyProperties exists {
        readOnlyProperties[*] {
            this IN %paths
            <<
            {
                "result": "NON_COMPLIANT",
                "check_id": "PR007",
                "message": "readOnlyProperties MUST have properties defined in the schema"
            }
            >>
        }
    }

    when writeOnlyProperties exists {
        writeOnlyProperties[*] {
            this IN %paths
            <<
            {
                "result": "NON_COMPLIANT",
                "check_id": "PR008",
                "message": "writeOnlyProperties MUST have properties defined in the schema"
            }
            >>
        }
    }
}



rule ensure_description_is_descriptive {
    description == /^Resource Type definition for/
    <<
    {
        "result": "WARNING",
        "check_id": "GN001",
        "message": "description should start with `Resource Type definition for ...`"
    }
    >>
}

# URLs should use https
rule ensure_sourceUrl_uses_https when sourceUrl exists {
    sourceUrl == /^https:/
    <<
    {
        "result": "WARNING",
        "check_id": "GN002",
        "message": "sourceUrl should use https protocol"
    }
    >>
}

rule ensure_default_replacementStrategy when replacementStrategy exists {
    replacementStrategy == 'create_then_delete'
    <<
    {
        "result": "WARNING",
        "check_id": "GN003",
        "message": "replacement strategy should not implement create_then_delete"
    }
    >>
}
