let read = readOnlyProperties
let create = createOnlyProperties
let write = writeOnlyProperties




rule ensure_primary_identifier_exists_and_not_empty
{
    primaryIdentifier exists
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "PID001",
        "message": "primaryIdentifier MUST exist"
    }
    >>
    primaryIdentifier not empty
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "PID002",
        "message": "primaryIdentifier MUST contain values"
    }
    >>
}

rule ensure_primary_identifier_is_read_or_create_only when ensure_primary_identifier_exists_and_not_empty
{
    primaryIdentifier[*] {
        this IN %read OR
        this IN %create
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "PID003",
            "message": "primaryIdentifier MUST be either readOnly or createOnly"
        }
        >>
    }
}



rule ensure_create_and_read_only_intersection_is_empty
{
    readOnlyProperties[*] {
        this !IN %create
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "PR001",
            "message": "read/createOnlyProperties MUST NOT have common properties"
        }
        >>
    }

    createOnlyProperties[*] {
        this !IN %read
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "PR002",
            "message": "create/readOnlyProperties MUST NOT have common properties"
        }
        >>
    }
}

rule ensure_write_and_read_only_intersection_is_empty
{
    readOnlyProperties[*] {
        this !IN %write
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "PR003",
            "message": "read/writeOnlyProperties MUST NOT have common properties"
        }
        >>
    }
    writeOnlyProperties[*] {
        this !IN %read
        <<
        {
            "result": "NON_COMPLIANT",
            "check_id": "PR004",
            "message": "write/readOnlyProperties MUST NOT have common properties"
        }
        >>
    }
}



rule ensure_description_is_descriptive {
    description != /^Resource Type definition for/
    <<
    {
        "result": "WARNING",
        "check_id": "GN001",
        "message": "description should start with `Resource Type definition for ...`"
    }
    >>
}

# URLs should use https
rule ensure_sourceUrl_uses_https when sourceUrl exists {
    sourceUrl == /^https:/
    <<
    {
        "result": "NON_COMPLIANT",
        "check_id": "GN002",
        "message": "sourceUrl should use https protocol"
    }
    >>
}

rule ensure_default_replacementStrategy when replacementStrategy exists {
    replacementStrategy == 'create_then_delete'
    <<
    {
        "result": "WARNING",
        "check_id": "GN003",
        "message": "replacement strategy should not implement create_then_delete"
    }
    >>
}
